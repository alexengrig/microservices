plugins {
    id 'org.springframework.boot' apply false
    id 'io.spring.dependency-management' apply false
}

subprojects {
    group = 'dev.alexengrig.microservice'
    version = '0.1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    pluginManager.withPlugin('java') {
        sourceCompatibility = '17'

        test {
            useJUnitPlatform()
        }
    }

    pluginManager.withPlugin('org.springframework.boot') {
        springBoot {
            buildInfo()
        }

        dependencies {
            implementation('org.springframework.boot:spring-boot-starter')
            testImplementation('org.springframework.boot:spring-boot-starter-test')

            implementation('org.springframework.boot:spring-boot-starter-actuator')

            if (project.name != 'config-server' && project.name != 'eureka-server') {
                implementation('org.springframework.cloud:spring-cloud-starter-config')
            }

            if (project.name != 'eureka-server') {
                implementation('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')
            }
            implementation('com.github.ben-manes.caffeine:caffeine')

            compileOnly('org.projectlombok:lombok')
            annotationProcessor('org.projectlombok:lombok')
            testCompileOnly('org.projectlombok:lombok')
            testAnnotationProcessor('org.projectlombok:lombok')
        }
    }

    pluginManager.withPlugin('io.spring.dependency-management') {
        dependencyManagement {
            imports {
                mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
            }
        }
    }
}
